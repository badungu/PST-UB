

// ================================================================================================
//                         PLANESCAPE: TORMENT UNFINISHED BUSINESS
//                             COMPONENT:  CANDLESTICK QUEST
//                                BY QWINN (Paul Escalona)
// ================================================================================================
// Last Modification Date:  2008/05/09 - v1.00
// ================================================================================================


// Originally restored and distributed as a stand alone restoration mod by Platter
// With several fixes by Skardavnelnate


// =================== LONGLIST.TXT DOCUMENTATION FROM SKARD'S FIXES ======================
// ================================ AS OF AIP 20080408 ====================================
// ------------------------------
// quests.ini
// ------------------------------
// These add the quest descriptions for finding Sheryl's father and retrieving a leg bone for Ulthera.

// [init]
// questcount=127

// [125]
// title=64151
// descAssigned=64940
// descCompleted=64941
// assignedChecks=1
// aVar1=Sheryl_Quest
// aValue1=3
// aCondition1=EQ
// completeChecks=1
// cVar1=Sheryl_Quest
// cValue1=4
// cCondition1=EQ

// [126]
// title=64943
// descAssigned=64944
// descCompleted=64945
// assignedChecks=1
// aVar1=Ulthera_Quest
// aValue1=3
// aCondition1=EQ
// completeChecks=1
// cVar1=Ulthera_Quest
// cValue1=4
// cCondition1=EQ

// AR0109.ARE
// Actor 11
// Enter name Ulthera

// SHERYL.CRE (taken from BURIEDF.CRE)
// Animation ID (cl 4) Townie, LC Female - 24637
// # colors 4
// Color 1 hair_Blond - 6
// Color 2 cloth_Brown_Green - 76
// Color 3 skin_Light - 41
// Color 4 leather_Olive_Green - 202
// Color 1 placement Hair - 224
// Color 2 placement Main cloth - 160
// Color 3 placement Skin - 176
// Color 4 placement Minor cloth - 144
// Color 5 placement Not used - 0
// Color 6 placement Not used - 0
// Color 7 placement Not used - 0

// ULTHERA.CRE (taken from MMOURN.CRE)
// Animation ID (cl 4) Midwife - 24664
// # colors 4
// Color 1 hair_Brassy_White - 9
// Color 2 cloth_Blue_Grey - 87
// Color 3 skin_Tan - 39
// Color 4 cloth_Light_Grey - 83
// Color 1 placement Hair - 224
// Color 2 placement Main cloth - 160
// Color 3 placement Skin - 176
// Color 4 placement Minor cloth - 144
// Color 5 placement Not used - 0
// Color 6 placement Not used - 0
// Color 7 placement Not used - 0

// DULTHERA.DLG
// Ask about Pharod after you found him.
// Add Response trigger Global("Pharod","GLOBAL",0)
// Response 32 set flag, add Trigger index 3

// The portal opens before you know it's location.
// The portal reopens while already open.
// AR0109.ARE
// cantrig1
// Flag Reset trap(1)

// CANTRIG.BCS
// Add Global("Ulthera_Quest","GLOBAL",4) as line 4
// Add BitSet("0109_Status","AR0109",BIT2) as line 7

// Add
// IF
//	HarmlessEntered("Nordom")
// THEN
// 	RESPONSE #100
// 		VerbalConstant("Nordom",NORDOM_PORTAL)
// 		Continue()
// END


// ============================= Begin Candlestick Quest ==================================

// COMPILE ~PST-UB/Candlestick/dlg/Candlestick.d~

INCLUDE ~PST-UB/utils/Q_AREMacros.tph~
COMPILE ~PST-UB/Candlestick/dlg/Candlestick.d~

COPY ~quests.ini~ ~quests.ini~
  COUNT_REGEXP_INSTANCES CASE_INSENSITIVE ~descAssigned=~ "Quest1"
  SET "NewTotal" = "Quest1" + 2
  SET "Quest2" = "Quest1" + 1
  REPLACE_TEXTUALLY
    ~questcount=%Quest1%~ ~questcount=%NewTotal%~
 BUT_ONLY_IF_IT_CHANGES

APPEND_OUTER ~quests.ini~
~
[%Quest1%]
title=64151
descAssigned=64940
descCompleted=64941
assignedChecks=1
aVar1=Sheryl_Quest
aValue1=3
aCondition1=EQ
completeChecks=1
cVar1=Sheryl_Quest
cValue1=4
cCondition1=EQ

[%Quest2%]
title=64943
descAssigned=64944
descCompleted=64945
assignedChecks=1
aVar1=Ulthera_Quest
aValue1=3
aCondition1=EQ
completeChecks=1
cVar1=Ulthera_Quest
cValue1=4
cCondition1=EQ
~  KEEP_CRLF

// =====================BEGIN AREA 0109 EDIT ===========================

// Adding 1 actor (0x110), 2 trigger points (0xc4 each, total 0x188), 10 vertexes (0x4 each, total 0x28)

COPY_EXISTING ~AR0109.ARE~ ~override~
  LAUNCH_PATCH_MACRO ~Q_ARE_InitVars~
  LAUNCH_PATCH_MACRO ~Q_AREAdd_InitVars~
  SET "Q_New_Actor" = 1
  SET "Q_New_Trigg" = 2
  SET "Q_New_Vertx" = 10
  LAUNCH_PATCH_MACRO ~Q_AREAdd_Process~

// Add Actor

  WRITE_ASCII "Q_NewOffset_Actor"           ~Ulthera~      #32 // Name
  WRITE_ASCII "Q_NewOffset_Actor" + 0x80    ~ULTHERA~      #8  // CRE
  WRITE_ASCII "Q_NewOffset_Actor" + 0x48    ~DULTHERA~     #8  // DLG
  WRITE_LONG  "Q_NewOffset_Actor" + 0x38    0xffffffff         // Unknown
  WRITE_SHORT "Q_NewOffset_Actor" + 0x20    1554               // Pos X
  WRITE_SHORT "Q_NewOffset_Actor" + 0x22    2026               // Pos Y
  WRITE_SHORT "Q_NewOffset_Actor" + 0x24    1554               // Des X
  WRITE_SHORT "Q_NewOffset_Actor" + 0x26    2026               // Des Y
  WRITE_SHORT "Q_NewOffset_Actor" + 0x34    9                  // Orientation
  WRITE_LONG  "Q_NewOffset_Actor" + 0x40    0x00ffffff         // Present
  WRITE_SHORT "Q_NewOffset_Actor" + 0x28    1                  // Visible

// Add 2 Triggers

  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0)         ~candle1~       #32 // Name
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x20  2                   // Travel Trigger Flag
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x22  695                 // Left
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x24  686                 // Top
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x26  804                 // Right
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x28  839                 // Bottom
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x2a  5                   // Num of Vertexes
  WRITE_LONG  "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x2c  "Q_Num_Vertx" - 10  // First Vertex Index
  WRITE_LONG  "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x34  48                  // Cursor Frame
  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x38  ~AR0207~         #8 // Destination Area
  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x40  ~fr0109~        #32 // Entrance name
  WRITE_LONG  "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x60  0x0004              // Party Required Flag
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x70  749                 // Trap Loc X
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x72  762                 // Trap Loc Y
  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x7c  ~~              #32 // Script Name (BCS)

  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1)         ~cantrig1~      #32 // Name
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x20  0                   // Proximity Trigger Flag
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x22  572                 // Left
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x24  697                 // Top
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x26  876                 // Right
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x28  918                 // Bottom
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x2a  5                   // Num of Vertexes
  WRITE_LONG  "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x2c  "Q_Num_Vertx" - 5   // First Vertex Index
  WRITE_LONG  "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x34  0                   // Cursor Frame
  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x38  ~~               #8 // Destination Area
  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x40  ~~              #32 // Entrance name
  WRITE_LONG  "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x60  0x0002              // Reset Trap Flag
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x70  724                 // Trap Loc X
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x72  807                 // Trap Loc Y
  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x7c  ~CANTRIG1~      #32 // Script Name (BCS)

// Add 10 Vertexes

  WRITE_SHORT "Q_NewOffset_Vertx" + 0   804
  WRITE_SHORT "Q_NewOffset_Vertx" + 2   779
  WRITE_SHORT "Q_NewOffset_Vertx" + 4   784
  WRITE_SHORT "Q_NewOffset_Vertx" + 6   686
  WRITE_SHORT "Q_NewOffset_Vertx" + 8   698
  WRITE_SHORT "Q_NewOffset_Vertx" + 10  736
  WRITE_SHORT "Q_NewOffset_Vertx" + 12  695
  WRITE_SHORT "Q_NewOffset_Vertx" + 14  839
  WRITE_SHORT "Q_NewOffset_Vertx" + 16  804
  WRITE_SHORT "Q_NewOffset_Vertx" + 18  779

  WRITE_SHORT "Q_NewOffset_Vertx" + 20  721
  WRITE_SHORT "Q_NewOffset_Vertx" + 22  918
  WRITE_SHORT "Q_NewOffset_Vertx" + 24  572
  WRITE_SHORT "Q_NewOffset_Vertx" + 26  865
  WRITE_SHORT "Q_NewOffset_Vertx" + 28  794
  WRITE_SHORT "Q_NewOffset_Vertx" + 30  697
  WRITE_SHORT "Q_NewOffset_Vertx" + 32  876
  WRITE_SHORT "Q_NewOffset_Vertx" + 34  809
  WRITE_SHORT "Q_NewOffset_Vertx" + 36  721
  WRITE_SHORT "Q_NewOffset_Vertx" + 38  918

 BUT_ONLY_IF_IT_CHANGES

// ==================== BEGIN AREA 0207 EDIT ===========================

// First, delete 4th trigger point (labelled Trigger Point 5 in original game)
// and it's vertexes, update any other vertex files' First Vertex Index.

COPY_EXISTING ~AR0207.ARE~ ~override~

  LAUNCH_PATCH_MACRO ~Q_ARE_InitVars~

  // No one assigned AR0207.BCS to be the area script for AR0207.ARE.
  WRITE_ASCII 0x94 ~AR0207~ #8

  // 0207ATTK script defined below in Qwinn section, replaces ENEMY.BCS
  WRITE_ASCII "Q_Off_Actor" + ("Q_Siz_Actor" * 0) + 0x70 ~0207ATTK~ #8
  WRITE_ASCII "Q_Off_Actor" + ("Q_Siz_Actor" * 1) + 0x70 ~0207ATTK~ #8
  WRITE_ASCII "Q_Off_Actor" + ("Q_Siz_Actor" * 2) + 0x70 ~0207ATTK~ #8

  LAUNCH_PATCH_MACRO ~Q_AREVertex_InitVars~
  SET "QV_Type" = 1
  SET "QV_VCount" = 0
  SET "QV_Offset" = ("Q_Off_Trigg" + ("Q_Siz_Trigg" * 3))
  LAUNCH_PATCH_MACRO ~Q_AREVertex_Process~

  LAUNCH_PATCH_MACRO ~Q_AREAdd_InitVars~
  DELETE_BYTES ("Q_Off_Trigg" + ("Q_Siz_Trigg" * 3))     "Q_Siz_Trigg"
  SET "Q_New_Trigg" = 0 - 1
  SET "Q_ManualInsert" = 1
  LAUNCH_PATCH_MACRO ~Q_AREAdd_Process~

// Now it's time to insert our proximity trigger for the portal exit as the 4th
// trigger point, and our actual travel trigger.  Our new trigger 4 has 7 vertexes, and
// our travel trigger has 9.

  LAUNCH_PATCH_MACRO ~Q_ARE_InitVars~
  LAUNCH_PATCH_MACRO ~Q_AREAdd_InitVars~
  SET "Q_New_Trigg" = 2
  SET "Q_New_Vertx" = 16
  LAUNCH_PATCH_MACRO ~Q_AREAdd_Process~

  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0)         ~cantrig2~      #32 // Name
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x20  0                   // Proximity Trigger
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x22  496                 // Left
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x24  428                 // Top
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x26  631                 // Right
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x28  542                 // Bottom
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x2a  7                   // Num of Vertexes
  WRITE_LONG  "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x2c  "Q_Num_Vertx" - 16  // First Vertex Index
  WRITE_LONG  "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x34  0                   // Cursor Frame
  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x38  ~~               #8 // Destination Area
  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x40  ~~              #32 // Entrance name
  WRITE_LONG  "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x60  0x0002              // Reset Trap Flag
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x70  561                 // Trap Loc X
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x72  485                 // Trap Loc Y
  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 0) + 0x7c  ~CANTRIG2~      #32 // Script Name (BCS)

  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1)         ~candle2~       #32 // Name
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x20  2                   // Travel Trigger
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x22  540                 // Left
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x24  430                 // Top
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x26  595                 // Right
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x28  526                 // Bottom
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x2a  9                   // Num of Vertexes
  WRITE_LONG  "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x2c  "Q_Num_Vertx" - 9   // First Vertex Index
  WRITE_LONG  "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x34  48                  // Cursor Frame
  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x38  ~AR0109~         #8 // Destination Area
  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x40  ~frommap~       #32 // Entrance name
  WRITE_LONG  "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x60  0x0004              // Party Required Flag
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x70  575                 // Trap Loc X
  WRITE_SHORT "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x72  463                 // Trap Loc Y
  WRITE_ASCII "Q_NewOffset_Trigg" + ("Q_Siz_Trigg" * 1) + 0x7c  ~~              #32 // Script Name (BCS)

  WRITE_SHORT "Q_NewOffset_Vertx" + 0   534
  WRITE_SHORT "Q_NewOffset_Vertx" + 2   430
  WRITE_SHORT "Q_NewOffset_Vertx" + 4   496
  WRITE_SHORT "Q_NewOffset_Vertx" + 6   462
  WRITE_SHORT "Q_NewOffset_Vertx" + 8   547
  WRITE_SHORT "Q_NewOffset_Vertx" + 10  542
  WRITE_SHORT "Q_NewOffset_Vertx" + 12  611
  WRITE_SHORT "Q_NewOffset_Vertx" + 14  542
  WRITE_SHORT "Q_NewOffset_Vertx" + 16  631
  WRITE_SHORT "Q_NewOffset_Vertx" + 18  502
  WRITE_SHORT "Q_NewOffset_Vertx" + 20  576
  WRITE_SHORT "Q_NewOffset_Vertx" + 22  428
  WRITE_SHORT "Q_NewOffset_Vertx" + 24  534
  WRITE_SHORT "Q_NewOffset_Vertx" + 26  430

  WRITE_SHORT "Q_NewOffset_Vertx" + 28  586
  WRITE_SHORT "Q_NewOffset_Vertx" + 30  457
  WRITE_SHORT "Q_NewOffset_Vertx" + 32  595
  WRITE_SHORT "Q_NewOffset_Vertx" + 34  495
  WRITE_SHORT "Q_NewOffset_Vertx" + 36  584
  WRITE_SHORT "Q_NewOffset_Vertx" + 38  526
  WRITE_SHORT "Q_NewOffset_Vertx" + 40  559
  WRITE_SHORT "Q_NewOffset_Vertx" + 42  515
  WRITE_SHORT "Q_NewOffset_Vertx" + 44  543
  WRITE_SHORT "Q_NewOffset_Vertx" + 46  491
  WRITE_SHORT "Q_NewOffset_Vertx" + 48  540
  WRITE_SHORT "Q_NewOffset_Vertx" + 50  462
  WRITE_SHORT "Q_NewOffset_Vertx" + 52  548
  WRITE_SHORT "Q_NewOffset_Vertx" + 54  439
  WRITE_SHORT "Q_NewOffset_Vertx" + 56  565
  WRITE_SHORT "Q_NewOffset_Vertx" + 58  430
  WRITE_SHORT "Q_NewOffset_Vertx" + 60  586
  WRITE_SHORT "Q_NewOffset_Vertx" + 62  457
 BUT_ONLY_IF_IT_CHANGES

// =================================================================================
// AR0903 EDIT

COPY_EXISTING ~AR0903.ARE~ ~override~
  LAUNCH_PATCH_MACRO ~Q_ARE_InitVars~
  WRITE_ASCII "Q_Off_Actor" + 0x70 ~~ #8
  WRITE_ASCII "Q_Off_Actor" + 0x80 ~SHERYL~ #8
 BUT_ONLY_IF_IT_CHANGES

// =================================================================================

COPY_EXISTING ~ARKRING.ITM~ ~override~
  WRITE_LONG 0x8 26009
  WRITE_LONG 0xc 26009
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~SHERYL.CRE~ ~override~
  WRITE_SHORT 0x28 24637  // Animation ID
  WRITE_ASCII 0x268 ~3004BTHG~ #8
  WRITE_BYTE  0x2df 4
  WRITE_SHORT 0x2e4 6
  WRITE_SHORT 0x2e6 76
  WRITE_SHORT 0x2e8 41
  WRITE_SHORT 0x2ea 202
  WRITE_SHORT 0x2ec 0
  WRITE_SHORT 0x2ee 0
  WRITE_SHORT 0x2f0 0
  WRITE_BYTE 0x2f5 224
  WRITE_BYTE 0x2f6 160
  WRITE_BYTE 0x2f7 176
  WRITE_BYTE 0x2f8 144
  WRITE_BYTE 0x2f9 0
  WRITE_BYTE 0x2fa 0
  WRITE_BYTE 0x2fb 0
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~ULTHERA.CRE~ ~override~
  WRITE_SHORT 0x28 24664  // Animation ID
  WRITE_ASCII 0x268 ~3004BTHG~ #8
  WRITE_BYTE  0x2df 4
  WRITE_SHORT 0x2e4 9
  WRITE_SHORT 0x2e6 87
  WRITE_SHORT 0x2e8 39
  WRITE_SHORT 0x2ea 83
  WRITE_SHORT 0x2ec 0
  WRITE_SHORT 0x2ee 0
  WRITE_SHORT 0x2f0 0

  WRITE_BYTE 0x2f5 224
  WRITE_BYTE 0x2f6 160
  WRITE_BYTE 0x2f7 176
  WRITE_BYTE 0x2f8 144
  WRITE_BYTE 0x2f9 0
  WRITE_BYTE 0x2fa 0
  WRITE_BYTE 0x2fb 0
 BUT_ONLY_IF_IT_CHANGES

<<<<<<<< .../inlinedfile/AR0207.INI
[nameless]
destare		= ar0207
point		= [0296.0578]
state		= 35
>>>>>>>>
COPY ~.../inlinedfile/AR0207.INI~ ~override~


// ==================================================================================================

// The above is work by either Platter or SKARDAVNELNATE.  The following is additional fixes and script
// rewrites by Qwinn.

// Creating "Candle1_Portal_Open" and "Candle2_Portal_Open" variables, instead of the bitset on
// 0109_Status that SKARDAVNELNATE did, since that variable doesn't exist.

// Rewriting all scripts.

INCLUDE ~PST-UB/utils/Q_VARMacros.tph~
COPY_EXISTING ~VAR.VAR~ ~VAR.TMP~
  SET Q_FileSize = %SOURCE_SIZE%
  SPRINT "Q_NewVarType"  ~AR0109  ~
  SPRINT "Q_NewVarName"  ~Candle1_Portal_Open             ~
  SET    Q_NewVarValue = 0
  LAUNCH_PATCH_MACRO Q_VarVar_AddNewRecord
  SPRINT "Q_NewVarType"  ~AR0207  ~
  SPRINT "Q_NewVarName"  ~Candle2_Portal_Open             ~
  SET    Q_NewVarValue = 0
  LAUNCH_PATCH_MACRO Q_VarVar_AddNewRecord
 BUT_ONLY_IF_IT_CHANGES
COPY ~VAR.TMP~ ~VAR.VAR~

// Picking a standard nomenclature rather than "ENEMY.BCS" for enemy script, rewrote it, assigned to the actors
// in area edit above
<<<<<<<< .../inlinedfile/0207ATTK.BAF
IF
  See([PC])
THEN
  RESPONSE #100
    Enemy()
    Attack([PC])
END
>>>>>>>>
COMPILE ~.../inlinedfile/0207ATTK.BAF~


<<<<<<<< .../inlinedfile/CANTRIG1.BAF
IF
  HarmlessEntered("Nordom")
THEN
  RESPONSE #100
    VerbalConstant("Nordom",NORDOM_PORTAL)
    Continue()
END

IF
  HarmlessEntered("Nameless")
  PartyHasItem("Candle") // Candlestick
  Global("Ulthera_Quest","GLOBAL",4)
THEN
  RESPONSE #100
    SetGlobal("Candle1_Portal_Open","AR0109",1)
    ActivatePortalCursor("Candle1",1)
    EnablePortalTravel("Candle1",1)
END
>>>>>>>>
COMPILE ~.../inlinedfile/CANTRIG1.BAF~


<<<<<<<< .../inlinedfile/CANTRIG2.BAF
IF
  HarmlessEntered("Nordom")
THEN
  RESPONSE #100
    VerbalConstant("Nordom",NORDOM_PORTAL)
    Continue()
END

IF
  HarmlessEntered("Nameless")
  PartyHasItem("Purse") // Leather Purse
THEN
  RESPONSE #100
    SetGlobal("Candle2_Portal_Open","AR0207",1)
    ActivatePortalCursor("Candle2",1)
    EnablePortalTravel("Candle2",1)
END
>>>>>>>>
COMPILE ~.../inlinedfile/CANTRIG2.BAF~

<<<<<<<< .../inlinedfile/closecantrig1.baf
IF
  Global("Candle1_Portal_Open","AR0109",1)
  !NearLocation(Protagonist,[749.762],20)
THEN
  RESPONSE #100
    SetGlobal("Candle1_Portal_Open","AR0109",0)
    ActivatePortalCursor("Candle1",0)
    EnablePortalTravel("Candle1",0)
    Continue()
END
>>>>>>>>
EXTEND_TOP ~AR0109.BCS~ ~.../inlinedfile/closecantrig1.baf~

<<<<<<<< .../inlinedfile/closecantrig2.baf
IF
  Global("Candle2_Portal_Open","AR0207",1)
  !NearLocation(Protagonist,[575.463],10)
THEN
  RESPONSE #100
    SetGlobal("Candle2_Portal_Open","AR0207",0)
    ActivatePortalCursor("Candle2",0)
    EnablePortalTravel("Candle2",0)
    Continue()
END
>>>>>>>>
EXTEND_TOP ~AR0207.BCS~ ~.../inlinedfile/closecantrig2.baf~
