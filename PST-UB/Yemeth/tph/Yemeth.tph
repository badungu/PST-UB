// =========================== Pendant of Yemeth Quest here =============================


// ==================== SKARDAVNELNATE'S RESTORED PENDANT OF YEMETH QUEST ===============
// ======================= FROM SKARDAVNELNATE'S AIP VERSION 20040408 ===================

// ================================ WEIDUIZATION BY QWINN ===============================

// Last Revision by Qwinn:  20080423

// LONGLIST DOCUMENTATION FROM SKARDAVNELNATE:

// ------------------------------
// Pendant of Yemeth Quest

// Three New Scripts
// 0400ADY.BCS
// 0400RC.BCS
// 0400RC2.BCS

<<<<<<<< .../inlinedfile/0400ADY.BAF
IF
  TimeOfDay(NIGHT)
THEN
  RESPONSE #100
    DestroySelf()
END

IF
  Help([0.0.TEAM_4])
THEN
  RESPONSE #100
    Enemy()
    Attack(LastAttackerOf(LastHelp))
END

IF
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Attack(NearestEnemyOf(Myself))
END

IF
  AttackedBy([PC],DEFAULT)
THEN
  RESPONSE #100
    Enemy()
    Attack(LastAttackerOf(Myself))
END
>>>>>>>>
COMPILE ~.../inlinedfile/0400ADY.BAF~


<<<<<<<< .../inlinedfile/0400RC.BAF
IF
  OnCreation()
THEN
  RESPONSE #100
    SetTeam(Myself,TEAM_4)
END

IF
  NumCreatureGT("RKCHSR",1)
THEN
  RESPONSE #100
    DestroySelf()
END

IF
  TimeOfDay(NIGHT)
  NumCreatureGT("RKCHSR2",0)
THEN
  RESPONSE #100
    DestroySelf()
END

IF
  TimeOfDay(NIGHT)
  NumCreatureLT("RKCHSR2",1)
THEN
  RESPONSE #100
    DestroySelf()
    CreateCreature("RKCHSR2",[3165.450]) // Rake-Chaser - Cursed Erinys
END

IF
  Die()
THEN
  RESPONSE #100
    CreateItem("SKULPEND",0,0,0) // Pendant of Yemeth
    DropInventory()
END

IF
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Help()
    RunAwayFrom([PC],50)
END

IF
  AttackedBy([PC],DEFAULT)
THEN
  RESPONSE #100
    Enemy()
    Help()
    RunAwayFrom([PC],50)
END
>>>>>>>>
COMPILE ~.../inlinedfile/0400RC.BAF~


<<<<<<<< .../inlinedfile/0400RC2.BAF
IF
  NumCreatureGT("RKCHSR2",1)
THEN
  RESPONSE #100
    DestroySelf()
END

IF
  !TimeOfDay(NIGHT)
  NumCreatureGT("RKCHSR",0)
THEN
  RESPONSE #100
    DestroySelf()
END

IF
  !TimeOfDay(NIGHT)
  NumCreatureLT("RKCHSR",1)
THEN
  RESPONSE #100
    DestroySelf()
    CreateCreature("RKCHSR",[3165.490]) // Rake-Chaser - Collector
END

IF
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    Help()
    RunAwayFrom([PC],50)
END

IF
  AttackedBy([PC],DEFAULT)
THEN
  RESPONSE #100
    Enemy()
    Help()
    RunAwayFrom([PC],50)
END
>>>>>>>>
COMPILE ~.../inlinedfile/0400RC2.BAF~


// ADYZOEL.CRE
// Default script 0400ADY.BCS

// RKCHSR.CRE
// Default script 0400RC.BCS

// RKCHSR2.CRE
// Default script 0400RC2.BCS

COPY_EXISTING ~ADYZOEL.CRE~ ~override~
  WRITE_ASCII 0x268 ~0400ADY~ #8
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~RKCHSR.CRE~ ~override~
  WRITE_ASCII 0x268 ~0400RC~ #8
 BUT_ONLY_IF_IT_CHANGES

COPY_EXISTING ~RKCHSR2.CRE~ ~override~
  WRITE_ASCII 0x268 ~0400RC2~ #8
 BUT_ONLY_IF_IT_CHANGES


// AR0400.BCS
<<<<<<<< .../inlinedfile/rakes.baf
IF
  !Global("Current_Area","GLOBAL",400)
  !TimeOfDay(NIGHT)
  !Exists("RKCHSR")
  !Exists("RKCHSR2")
  GlobalLT("RKCHSR_DEAD","KAPUTZ",1)
  GlobalLT("RKCHSR2_DEAD","KAPUTZ",1)
THEN
  RESPONSE #100
    CreateCreature("RKCHSR",[3165.490]) // Rake-Chaser - Collector
END

IF
  !TimeOfDay(NIGHT)
  !Exists("ADYZOEL")
  GlobalLT("ADYZOEL_DEAD","KAPUTZ",1)
  GlobalLT("RKCHSR_DEAD","KAPUTZ",1)
  GlobalLT("RKCHSR2_DEAD","KAPUTZ",1)
THEN
  RESPONSE #100
    CreateCreature("ADYZOEL",[3135.590]) // Adyzoel Eriane-Ard - Rake
END

IF
  !Global("Current_Area","GLOBAL",400)
  TimeOfDay(NIGHT)
  !Exists("RKCHSR")
  !Exists("RKCHSR2")
  GlobalLT("RKCHSR_DEAD","KAPUTZ",1)
  GlobalLT("RKCHSR2_DEAD","KAPUTZ",1)
THEN
  RESPONSE #10
    CreateCreature("RKCHSR2",[3165.450]) // Rake-Chaser - Cursed Erinyes
END
>>>>>>>>
EXTEND_TOP ~AR0400.BCS~ ~.../inlinedfile/rakes.baf~


// Export 0101RATB.BCS
// Rename RATBONE.BCS
// Paste in override
// Replace lines 29-37 with
COPY_EXISTING ~0101RATB.BCS~ ~override/RATBONE.BCS~
  DECOMPILE_BCS_TO_BAF
   REPLACE_TEXTUALLY
 ~RESPONSE #15
    FloatMessage(Myself,48282)
  RESPONSE #15
    PlaySequence(ANIM_STANCEFIDGET1)
    PlaySequence(ANIM_STANCETOSTAND)
  RESPONSE #10
    FloatMessage(Myself,48282)
    PlaySequence(ANIM_STANCEFIDGET1)
    PlaySequence(ANIM_STANCETOSTAND)~
 ~RESPONSE #20
    Wait(5)
    PlaySequence(ANIM_TALK)
    FloatMessage(Myself,10572)
    Wait(5)
    PlaySequence(ANIM_TALK)
    FloatMessage(Myself,10573)
    Wait(5)
    PlaySequence(ANIM_TALK)
    FloatMessage(Myself,10574)
    Wait(4)
  RESPONSE #20
    Wait(5)
    PlaySequence(ANIM_TALK)
    FloatMessage(Myself,14818)
    Wait(5)
    PlaySequence(ANIM_TALK)
    FloatMessage(Myself,14819)
    Wait(5)
    PlaySequence(ANIM_TALK)
    FloatMessage(Myself,14820)
    Wait(5)
    PlaySequence(ANIM_TALK)
    FloatMessage(Myself,14821)
    Wait(5)
    PlaySequence(ANIM_TALK)
    FloatMessage(Myself,14822)
    Wait(4)~
  COMPILE_BAF_TO_BCS
 BUT_ONLY_IF_IT_CHANGES

// DLTCEP
// Edit Areas
// Load Area AR0101
// Actors
// Add actor
// Creature RATBONE
// Refresh fields
// Position 1055 1520
// Desination 1055 1520
// Direction 2
// Dialog DRATBONE
// Default RATBONE

// AR0101.ARE
// Actor 5
// Name Ratbone

// DRATBONE.DLG
// Copy responses 242, 245, 246, 244
// Paste in that order
// State 74
// First transition index 256
// # transitions 4

// DTEGARIN.DLG
// Speaking with Tegar'in sends you to Aethelgrin's dialog, speaking with Aethelgrin sends you to
// Tegar'in's dialog which is missing some options.
// Copy responses 10-23 from DAETHEL.DLG
// Paste in DTEGARIN.DLG and sort
// State 8
// First transition index 102
// # transitions 14
// Copy Response triggers 2-8 from DAETHEL.DLG
// Paste in DTEGARIN.DLG and sort
// Response 108 Trigger index 11
// Response 109 Trigger index 12
// Response 110 Trigger index 13
// Response 111 Trigger index 14
// Response 112 Trigger index 15
// Response 113 Trigger index 16
// Response 114 Trigger index 17

// DRKCHSR2.DLG
// Know Rake-Chaser's identity before you're told.
// Add Response trigger Global("RC_ID","GLOBAL",3)
// Response 6, 7 set flag, add  trigger 15

// DAETHEL.DLG
// Know Rake-Chaser's identity.
// Action 2 SetGlobal("RC_ID","GLOBAL",3) -> SetGlobal("RC_ID","GLOBAL",2)

// END

// ======================== BEGIN PENDANT OF YEMETH QUEST =======================

INCLUDE ~PST-UB/utils/Q_AREMacros.tph~
COMPILE ~PST-UB/Yemeth/dlg/Yemeth.d~


COPY_EXISTING ~AR0101.ARE~ ~override~
  LAUNCH_PATCH_MACRO ~Q_ARE_InitVars~
  LAUNCH_PATCH_MACRO ~Q_AREAdd_InitVars~
  SET "Q_New_Actor" = 1
  LAUNCH_PATCH_MACRO ~Q_AREAdd_Process~
WRITE_ASCII ("Q_NewOffset_Actor" + ("Q_Siz_Actor" * 0))           ~Ratbone~        #32 // Name
WRITE_ASCII ("Q_NewOffset_Actor" + ("Q_Siz_Actor" * 0)) + 0x80    ~RATBONE~        #8  // CRE
WRITE_ASCII ("Q_NewOffset_Actor" + ("Q_Siz_Actor" * 0)) + 0x48    ~DRATBONE~       #8  // DLG
WRITE_ASCII ("Q_NewOffset_Actor" + ("Q_Siz_Actor" * 0)) + 0x70    ~RATBONE~        #8  // Default
WRITE_LONG  ("Q_NewOffset_Actor" + ("Q_Siz_Actor" * 0)) + 0x38    0xffffffff           // Unknown
WRITE_SHORT ("Q_NewOffset_Actor" + ("Q_Siz_Actor" * 0)) + 0x20    1055                 // Pos X
WRITE_SHORT ("Q_NewOffset_Actor" + ("Q_Siz_Actor" * 0)) + 0x22    1520                 // Pos Y
WRITE_SHORT ("Q_NewOffset_Actor" + ("Q_Siz_Actor" * 0)) + 0x24    1055                 // Des X
WRITE_SHORT ("Q_NewOffset_Actor" + ("Q_Siz_Actor" * 0)) + 0x26    1520                 // Des Y
WRITE_SHORT ("Q_NewOffset_Actor" + ("Q_Siz_Actor" * 0)) + 0x34    2                    // Orientation
WRITE_LONG  ("Q_NewOffset_Actor" + ("Q_Siz_Actor" * 0)) + 0x40    0x00ffffff           // Present
WRITE_SHORT ("Q_NewOffset_Actor" + ("Q_Siz_Actor" * 0)) + 0x28    1                    // Visible
 BUT_ONLY_IF_IT_CHANGES

// ================================================================================

// The above is the extent of SKARD's changes.  What follows are changes by Qwinn.

// If you install the quest as is, there are two Ratbones in Ragpicker's Square.
// The one that came with the vanilla game is the NPC that stands outside Sharegrave's
// kip and can train you to be a thief.  This quest restores the other one.  Having two toons
// with the same name, and a name like that no less, is a bit confusing and, well, lame.
// Not to mention they use the same .CRE, and I'd hate to see what happened if you killed
// one and not the other.

// I rename the thief-trainer Ratbone to Carver, and make the changes in the Fixpack
// so as to avoid confusion in future bug reports regarding either character in either
// mod.









